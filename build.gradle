group 'com.ssn'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
        dockerPluginVersion = '3.2.1'
        junitVersion = '5.0.0'
        junitPluginVersion = '1.0.2'
    }

    repositories {
        jcenter()
        mavenCentral()

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "com.bmuschko:gradle-docker-plugin:${dockerPluginVersion}"
        classpath "org.junit.platform:junit-platform-gradle-plugin:${junitPluginVersion}"

    }
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "http://dl.bintray.com/aatarasoff/maven" }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-netflix-core:1.3.6.RELEASE"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jar {
        archiveName = project.name + ".jar"
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-devtools")

        compileOnly("org.projectlombok:lombok")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$junitVersion"
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "$junitVersion"
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$junitVersion"
    }

    task dockerBuild {
        doLast {
            exec {
                commandLine 'docker', 'build', '--force-rm=true', "--tag='${project.name}'", '.'
            }
        }
    }

}